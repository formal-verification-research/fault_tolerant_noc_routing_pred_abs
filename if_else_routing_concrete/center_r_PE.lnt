module center_r_PE(datatypes, route_concrete) is

process center_r_PE [out_w, out_s, out_n, out_e, fail : any](node_loc : Coordinates) is
   loop
      var one_flit : Flit, flit_dest: Coordinates, x, y : Nat in
	 x := any Nat where (x < 3 of Nat);
	 y := any Nat where (y < 3 of Nat);
	 one_flit := body(Coordinates(NodeRange(x), NodeRange(y)));
	 flit_dest := get_dest(one_flit);
	 if (get_y(flit_dest)<get_y(node_loc)) then  -- y < j
   	    -- first south, then west
	    route_concrete[out_s, out_w, fail](one_flit)
   	 elsif (get_y(flit_dest)==get_y(node_loc)) and (get_x(flit_dest)==(get_x(node_loc)+1)) then -- y = j and x = (i+1)
   	    -- first east, then south
	    route_concrete[out_e, out_s, fail](one_flit)
	 elsif (get_y(flit_dest)==get_y(node_loc)) and (get_x(flit_dest)>(get_x(node_loc)+1)) then -- y = j and x > (i+1)
   	    -- first south, then east
	    route_concrete[out_s, out_e, fail](one_flit)
	 elsif (get_y(flit_dest)>=get_y(node_loc)) and (get_x(flit_dest)<get_x(node_loc)) then -- y >= j and x < i
	    -- first west, then south
	    route_concrete[out_w, out_s, fail](one_flit)
	 elsif (get_y(flit_dest)>get_y(node_loc)) and (get_x(flit_dest)>(get_x(node_loc)+1)) then -- y > j and x > (i+1)
	    -- first east, then north
	    route_concrete[out_e, out_n, fail](one_flit)
	 elsif (get_y(flit_dest)>get_y(node_loc)) and (get_x(flit_dest)==(get_x(node_loc)+1)) then -- y > j and x = (i+1)
	    -- first north, then east
    	    route_concrete[out_n, out_e, fail](one_flit)
	 elsif (get_y(flit_dest)==(get_y(node_loc)+1)) and (get_x(flit_dest)==get_x(node_loc)) then -- y = (j+1) and x = i
	    -- first north, then west
	    route_concrete[out_n, out_w, fail](one_flit)
	 elsif (get_y(flit_dest)>(get_y(node_loc)+1)) and (get_x(flit_dest)==get_x(node_loc)) then -- y > (j+1) and x = i
	    -- first west, then north
	    route_concrete[out_w, out_n, fail](one_flit)
   	 end if
      end var
   end loop
end process

end module