module nw_corner_r_S(datatypes, route_concrete) is

process nw_corner_r_S [input, out_pe, out_s, out_e, fail, drop : any] (node_loc : Coordinates) is
   loop
      var one_flit : Flit, flit_dest : Coordinates, arb_status : Bool in
	 input(?one_flit);
	 flit_dest := get_dest(one_flit);
	 if (get_y(flit_dest)==get_y(node_loc)) and (get_x(flit_dest)==get_x(node_loc)) then -- y = j, x = i
	    -- destination reached
	    out_pe(one_flit)
	 elsif (get_y(flit_dest)==get_y(node_loc)) and (get_x(flit_dest)==(get_x(node_loc)+1)) then -- y = j and x = (i+1)
	    -- first east, then south
	    route_concrete[out_e, out_s, drop](one_flit)
	 elsif (get_y(flit_dest)==get_y(node_loc)) and (get_x(flit_dest)>(get_x(node_loc)+1)) then -- y = j and x > (i+1)
	    -- only east	    
	    -- Destination is on the east (more than one node away to
	    -- the east) of (i,j), and the packet has detoured once.
	    out_e(?arb_status);
	    if arb_status == false then
	       fail(one_flit)
	    else
	       out_e(one_flit)
	    end if
	 elsif (get_y(flit_dest)<get_y(node_loc)) and (get_x(flit_dest)>get_x(node_loc)) then -- y < j and x > i					   
	    -- only east
	    out_e(?arb_status);
	    if arb_status == false then
	       fail(one_flit)
	    else
	       out_e(one_flit)
	    end if
	    -- the case below is covered by the one above.
	 (* elsif (get_y(flit_dest)<get_y(node_loc)) and (get_x(flit_dest)==(get_x(node_loc)+1)) then -- y < j and x = (i+1) *)
	 (*    -- Destination is on the southeast of (i,j) *)
	 (*    -- and is only one step away from (i,j). *)
	 (*    -- The packet has detoured at the last step. *)
	 (*    -- only east *)
	 (*    out_e(?arb_status); *)
	 (*    if arb_status == false then *)
	 (*       fail(one_flit) *)
	 (*    else *)
	 (*       out_e(one_flit) *)
	 (*    end if *)	    
	 end if	
      end var      
   end loop
end process -- nw_corner_r_S

end module -- nw_corner_r_S
