module s_edge_r_N(datatypes, route_concrete) is

process s_edge_r_N [input, out_pe, out_w, out_n, out_e, fail : any](node_loc : Coordinates) is
   loop
      var one_flit : Flit, flit_p : Bool, flit_r : Bool, flit_s : Bool, arb_status : Bool in
	 input(?one_flit);

     flit_p := get_p(one_flit); -- x > 1
     flit_r := get_r(one_flit); -- x < 1
     flit_s := get_s(one_flit); -- y < 1

     if (flit_s == true) and (flit_p == false) and (flit_r == false) then
	    out_pe(one_flit) -- destination reached
     elsif (flit_s == true) and (flit_p == true) then
	    -- first east, then north
	    route_concrete[out_e, out_n, fail](one_flit)
     elsif (flit_s == false) and (flit_p == true) then
    	    -- first east, then north
	    -- Destination is on the northeast of (i,j). Packet over-shooting
	    route_concrete[out_e, out_n, fail](one_flit)
     elsif (flit_s == true) and (flit_r == true) then
	    -- first west, then north
	    route_concrete[out_w, out_n, fail](one_flit)
     elsif (flit_s == false) and ((flit_r == true) or ((flit_p == false) and (flit_r == false))) then
	    -- only west
	    -- Destination is on the north or northwest of (i,j).
	    -- Packet has detoured.        
	    out_w(?arb_status);
	    if arb_status == false then
	       fail(one_flit)	       
	    else
	       out_w(one_flit)
	    end if
	 end if
      end var
   end loop
end process

end module
