module center_r_W(datatypes, route_concrete) is

process center_r_W [input, out_ip, out_s, out_n, out_e, fail, drop : any](node_loc : Coordinates) is
   loop
      var one_flit : Flit, flit_p, flit_q, flit_r, flit_s, arb_status : Bool in
	 input(?one_flit);

     flit_p := get_p(one_flit); -- x > 1
     flit_q := get_q(one_flit); -- y > 1
     flit_r := get_r(one_flit); -- x < 1
     flit_s := get_s(one_flit); -- y < 1

     if (flit_p == false) and (flit_q == false) and (flit_r == false) and (flit_s == false) then
	    out_ip(one_flit) -- destination reached
     elsif (flit_q == true) and (flit_p == true) then
	    -- first east, then north
	    route_concrete[out_e, out_n, fail](one_flit)
     elsif (flit_q == true) and (flit_p == false) and (flit_r == false) then
	    -- first north, then east
	    route_concrete[out_n, out_e, fail](one_flit)
     elsif (flit_q == false) and (flit_s == false) and (flit_p == true) then
	    -- only east
	    -- Destination is on the east of (i,j) and packet has detoured.
	    out_e(?arb_status);
	    if arb_status == false then
	       fail(one_flit)	       
	    else
	       out_e(one_flit)
	    end if
     elsif (flit_q == true) and (flit_r == true) then
	    -- only north
	    -- Destination is on the northwest of (i,j) and is one row above (i,j). Packet has detoured at the last step.
    	    out_n(?arb_status);
	    if arb_status == false then
	       fail(one_flit)	       
	    else
	       out_n(one_flit)
	    end if
     elsif (flit_s == true) then
	    -- Destination is on the southeast, south, or southwest of (i,j) and packet has detoured at the
            -- last step. Illegal turn.
    	    out_s(?arb_status);
	    if arb_status == false then
	       drop(one_flit)	       
	    else
	       out_s(one_flit)
	    end if
     elsif (flit_q == true) and (flit_p == false) and (flit_r == false) then
	    -- only north
	    -- special case: This node is on the column that is one step east to the west edge.
            -- Destination is on the northwest of (i,j) and packet has detoured from W edge.
      	    out_n(?arb_status);
	    if arb_status == false then
	       fail(one_flit)	       
	    else
	       out_n(one_flit)
	    end if
	 end if
      end var
   end loop
end process

end module
